resources
| where type =~ 'microsoft.compute/virtualmachines'

// How should resources be grouped?
// Choose an option below and uncomment only the appropriate section.

// Option 1:  Uncomment THE FOLLOWING SECTION to group resources by name prefix.
//            Example: 'vmgroup01' name prefix is 'vmgroup'

// | where name matches regex '^[a-zA-Z]+\\d+$'             
// | extend group = tostring(extract('^[a-zA-Z]+', 0, name)) 

// Option 2:  Uncomment THE FOLLOWING SECTION to group resources by regex pattern. 
//            Replace 'regex-pattern' with your regex pattern.
//            The pattern should contain one and only one capture group.
//            Use Copilot to quickly build regex patterns using sample resource names.

// | extend group = tostring(extract_all(@'(?i)regex-pattern', name))

// Option 3:  Uncomment THE FOLLOWING SECTION to group resources by resource group.

// | extend group = resourceGroup

// Option 4:  Uncomment THE FOLLOWING SECTION to group resources by resource tag.
//            Replace 'tag-name' with your tag name.

// | extend group = tostring(tags['tag-name'])

| where isnotempty(group)
| summarize vmCount = count()
            by group, location, subscriptionId, vmZone = tostring(zones[0])
| summarize _totalZones = countif(isnotempty(vmZone)),
            _totalZonalVms = sumif(vmCount, isnotempty(vmZone)),
            _totalRegionalVms = sumif(vmCount, isempty(vmZone)),
            _zonalSpreadPct = round(100 - (((stdevif(vmCount, isnotempty(vmZone)) / sumif(vmCount, isnotempty(vmZone))) * 100)), 2)
            by group, location, subscriptionId
| project group, location, subscriptionId,
          totalVms = _totalZonalVms + _totalRegionalVms,
          totalZonalVms = _totalZonalVms,
          totalRegionalVms = iff((_totalRegionalVms > 0), strcat(_totalRegionalVms, ' ⚠️'), tostring(_totalRegionalVms)),
          totalZones = iff((_totalZones < min_of(2, _totalZonalVms)), strcat(_totalZones, ' ⚠️'), tostring(_totalZones)),
          zonalSpreadScore = iff((_zonalSpreadPct < 80), strcat(_zonalSpreadPct, ' % ⚠️'), strcat(_zonalSpreadPct, ' %'))
