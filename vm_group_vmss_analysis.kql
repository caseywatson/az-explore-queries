resources
| where type =~ 'microsoft.compute/virtualmachines'

// How should resources be grouped?
// Choose an option below and uncomment only the appropriate section.

// Option 1:  Uncomment THE FOLLOWING SECTION to group resources by name prefix.
//            Example: 'vmgroup01' name prefix is 'vmgroup'

// | where name matches regex '^[a-zA-Z]+\\d+$'             
// | extend group = tostring(extract('^[a-zA-Z]+', 0, name)) 

// Option 2:  Uncomment THE FOLLOWING SECTION to group resources by regex pattern. 
//            Replace 'regex-pattern' with your regex pattern.
//            The pattern should contain one and only one capture group.
//            Use Copilot to quickly build regex patterns using sample resource names.

// | extend group = tostring(extract_all(@'(?i)regex-pattern', name))

// Option 3:  Uncomment THE FOLLOWING SECTION to group resources by resource group.

// | extend group = resourceGroup

// Option 4:  Uncomment THE FOLLOWING SECTION to group resources by resource tag.
//            Replace 'tag-name' with your tag name.

// | extend group = tostring(tags['epic-app'])

// Option 5:  Uncomment THE FOLLOWING SECTION to provide your own grouping expression.
//            Replace '// grouping expression' with your own valid KQL grouping expression.
//            Your grouping expression should return a single string.

// | extend group = // grouping expression

| where isnotempty(group)
| extend vmssId = tostring(properties.virtualMachineScaleSet.id)
| summarize _totalVms = count(),
            _totalVmsInVmss = countif(isnotempty(vmssId)),
            _totalVmsNotInVmss = countif(isempty(vmssId)),
            _totalVmss = dcountif(vmssId, isnotempty(vmssId))
            by group, location
| project _totalVms, _totalVmsInVmss, _totalVmsNotInVmss, _totalVmss, group, location,
          alert_totalVmsNotInVmss = tobool(_totalVmsNotInVmss > 0),
          alert_totalVmss = tobool(_totalVms > 0 and _totalVmss != 1)
| project group, location,
          totalVms = _totalVms,
          totalVmsInVmss = _totalVmsInVmss,
          totalVmsNotInVmss = iff(alert_totalVmsNotInVmss, strcat(_totalVmsNotInVmss, ' ⚠️'), tostring(_totalVmsNotInVmss)),
          totalVmss = iff(alert_totalVmss, strcat(_totalVmss, ' ⚠️'), tostring(_totalVmss)),
          notes = strcat(iff(alert_totalVmsNotInVmss, '⚠️ Some VMs in this group are not part of a VMSS. ', '✅ All VMs in this group are protected by VMSS. '),
                         iff(alert_totalVmss, '⚠️ All VMs in this group should be part of the same VMSS. ', '✅ All VMs in this group are part of the same VMSS. '))
